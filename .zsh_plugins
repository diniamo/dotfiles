update-custom-plugins() {
	if [[ $1 = "omz update"* ]]; then
		echo Updating plugins
		for plugin in $custom_plugins; do
			split=("${(@s|/|)plugin}")
			git -C "$ZSH/custom/plugins/$split[2]" pull
		done
	fi
}
autoload -U add-zsh-hook
add-zsh-hook preexec update-custom-plugins

collect-garbage() {
	# The -gt check doens't work if the array is empty
	# I'll give the person who can figure out what the obfuscation means a cookie
	enabled_custom_plugins=(dummy-lpafpndmtusw)
	for plugin in $custom_plugins; do
		pre_split=("${(s|/|)plugin}")
		split=("${(s|:|)pre_split[2]}")

		enabled_custom_plugins=($enabled_custom_plugins $split[1])
	done

	for folder in $ZSH/custom/plugins/*(/); do
		name=${folder##*/}
		if [[ ${enabled_custom_plugins[(ie)$name]} -gt ${#enabled_custom_plugins} ]]; then
			/usr/bin/rm -rf $folder
		fi
	done
}

for plugin in $custom_plugins; do
	pre_split=("${(s|/|)plugin}")
	split=("${(s|:|)pre_split[2]}")
	split=($pre_split[1] $split)

	if [ ! -d "$ZSH/custom/plugins/$split[-1]" ]; then
		git clone "https://github.com/$split[1]/$split[2].git" "$ZSH/custom/plugins/$split[-1]"
	fi

	plugins=($plugins $split[-1])
done
